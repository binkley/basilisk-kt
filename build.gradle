import java.nio.file.Files
import java.nio.file.Paths

import static java.nio.file.Files.createSymbolicLink

buildscript {
    repositories {
        gradlePluginPortal()
        mavenCentral()
        jcenter()
        maven {
            url "https://oss.sonatype.org/content/repositories/snapshots"
        }
        maven {
            url "https://dl.bintray.com/kotlin/kotlin-eap"
        }
        maven {
            url "https://dl.bintray.com/kotlin/exposed"
        }
        maven {
            url "https://jitpack.io"
        }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}"
        classpath "org.jetbrains.kotlin:kotlin-allopen:${kotlinVersion}"
    }
}

plugins {
    id "base"
    id "build-dashboard"

    id "com.github.ben-manes.versions"
    id "se.patrikerdes.use-latest-versions"
    id "com.dorongold.task-tree"

    id "io.spring.dependency-management"
}

gradle.afterProject { project ->
    linkPrePush(project)
}

allprojects {
    apply plugin: "io.spring.dependency-management"

    defaultTasks "build"
}

subprojects {
    buildscript {
        repositories {
            gradlePluginPortal()
            mavenCentral()
            jcenter()
            maven {
                url "https://oss.sonatype.org/content/repositories/snapshots"
            }
            maven {
                url "https://dl.bintray.com/kotlin/kotlin-eap"
            }
            maven {
                url "https://dl.bintray.com/kotlin/exposed"
            }
            maven {
                url "https://jitpack.io"
            }
        }

        dependencies {
            classpath "com.github.jengelman.gradle.plugins:shadow:${shadowVersion}"
            classpath "gradle.plugin.com.gorylenko.gradle-git-properties:gradle-git-properties:${gitPropertiesVersion}"
            classpath "gradle.plugin.com.pasam.gradle.buildinfo:spring-boot-build-info:${springBootBuildInfoVersion}"
        }
    }

    apply plugin: "org.jetbrains.kotlin.jvm"
    apply plugin: "org.jetbrains.kotlin.kapt"
    apply plugin: "org.jetbrains.kotlin.plugin.allopen"

    version = "0.1"
    group = "hm.binkley"

    repositories {
        mavenCentral()
        jcenter()
        maven {
            url "https://oss.sonatype.org/content/repositories/snapshots"
        }
        maven {
            url "https://dl.bintray.com/kotlin/kotlin-eap"
        }
        maven {
            url "https://dl.bintray.com/kotlin/exposed"
        }
        maven {
            url "https://jitpack.io"
        }
    }

    tasks.withType(Test) {
        systemProperty "java.util.logging.config.file", "${project.rootDir}/config/logging.properties"

        failFast = true

        useJUnitPlatform()

        testLogging {
            showExceptions = true
            showStandardStreams = true
        }
    }
}

build {
    finalizedBy buildDashboard
}

wrapper {
    distributionType = Wrapper.DistributionType.ALL
    gradleVersion = "${gradleWrapperVersion}"
}

static void linkPrePush(project) {
    def link = Paths.get("${project.rootDir}", ".git", "hooks", "pre-push")
    def target = Paths.get("${project.rootDir}", "pre-push")
    if (!Files.exists(link)) createSymbolicLink(link, target)
}
